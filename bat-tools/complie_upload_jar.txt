@echo off
echo ------------------------------------
echo 开始编译代码并上传至服务器
rem 1.ssh-keygen -t rsa
rem 2.cat id_dsa.pub >> ~/.ssh/authorized_keys (目标机器)
rem 3.vi /etc/ssh/sshd_config 按需要修改目录机器sshd服务配置
rem 4.systemctl restart sshd.service | systemctl status sshd.service
echo -------------------------------------

set bosent_git_user=fang_houjun
set bosent_git_pwd=
set bosent_git_url= http://172.16.101.154/BRM_cpghsyb/bosent1611_pcms.git
set bosent_git_branch=bussiness-platform-product-springcloud
set maven_cfg_dir=E:\maven_setting_cfg\cip-sofa.xml

set rsa_key_file=C:\Users\fang_houjun\.ssh\id_rsa
set ssh_ipaddr=10.30.32.12
set ssh_port=8920
set ssh_user=root
set ssh_remote_dir=/opt/pcms-springcloud

rem 工作目录
set bosent_wks=E:\business_platform\pcms_springcloud_20200526


rem 目前一共有如下channalcatalog,paymethod,pricemanager,productmix,quotamanager,workeffort,workflow项目不需要生成可以去掉以加快构建速度
set product_prj_name=channalcatalog,productmix





if exist %bosent_wks% (
if exist %bosent_wks%\bosent1611_pcms goto updateGitCode
del /S /Q %bosent_wks%\*.* &echo 成功删除目录下文件
rd  /S /Q %bosent_wks% & echo 成功删除工作空间目录:%bosent_wks%
)
mkdir %bosent_wks% & echo 成功创建工作空间目录：%bosent_wks%
cd /D %bosent_wks%
set bosent_git_url=%bosent_git_url:http://=%
git clone -b %bosent_git_branch%  http://%bosent_git_user%:%bosent_git_pwd%@%bosent_git_url% --depth 1


:updateGitCode
cd /D %bosent_wks%\bosent1611_pcms
call git pull


echo 代码已同步,按任意键进行工程代码编译
pause

if exist d:\batchput.bat (
	echo 检测试到批处理文件batchput准备删除
	del /F /Q d:\batchput.bat
)

rem 开启延迟变量
@setlocal enableextensions enabledelayedexpansion
set bak-jar=
set kill-jar=
set start-jar=
for %%A in (%product_prj_name%) do (
echo 正进入目录 %bosent_wks%\bosent1611_pcms\bosent_baseline_hot-deploy-SPRINGCLOUD-pcms%%A 开始编译
cd /D %bosent_wks%\bosent1611_pcms\bosent_baseline_hot-deploy-SPRINGCLOUD-pcms%%A
rem call mvn clean install --settings %maven_cfg_dir%
echo 开始生成put执行至batchput.bat....
echo put %bosent_wks%\bosent1611_pcms\bosent_baseline_hot-deploy-SPRINGCLOUD-pcms%%A\target\bosent_baseline_hot-deploy-SPRINGCLOUD-pcms%%A-161102-08-0.1-executable.jar  %ssh_remote_dir%/pcms-%%A >> d:\batchput.bat

set bak-jar="cd %ssh_remote_dir%/pcms-%%A;mv *.jar ./jar-bak/;echo 备份原jar包成功"!bak-jar!
set kill-jar="ps -ef | grep bosent_baseline_hot-deploy-SPRINGCLOUD-pcms%%A-161102-08-0.1-executable.jar | grep -v grep | awk '{print $2}' | xargs kill -9"!kill-jar!
set start-jar="cd %ssh_remote_dir%/pcms-%%A;nohup java -jar -Xms512m -Xmx1024m bosent_baseline_hot-deploy-SPRINGCLOUD-pcms%%A-161102-08-0.1-executable.jar>nohup.out 2>&1 & tail -f nohup.out|sed '/Started BosentStartforSpringCloud/Q'"!start-jar!
)

rem 将其中的""换成;   %A:B=C%  // dos中 A:B=C的方式来替换字符串，具体含义就是在字符串变量A中查找所有的子串B并且替换成子串C
set bak-jar=%bak-jar:""=;%
set kill-jar=%kill-jar:""=;%
set start-jar=%start-jar:""=;%


echo -------------即将进行执行队列------------------
echo bak-jar:%bak-jar%
echo kill-jar:%kill-jar%
echo start-jar:%start-jar%
echo -------------即将进行执行队列------------------

echo 按任意键开始备份原JAR......
pause

cd /D E:\OpenSSH-Win64
ssh -p %ssh_port% -i %rsa_key_file% %ssh_user%@%ssh_ipaddr% -t %bak-jar%

echo 开始上传jar至远程.....
pause

cd /D E:\OpenSSH-Win64
echo sftp -P %ssh_port% -i %rsa_key_file% -b d:\batchput.bat %ssh_user%@%ssh_ipaddr%
sftp -P %ssh_port% -i %rsa_key_file% -b d:\batchput.bat %ssh_user%@%ssh_ipaddr%

echo 开始KILL原服务....
pause

cd /D E:\OpenSSH-Win64
ssh -p %ssh_port% -i %rsa_key_file% %ssh_user%@%ssh_ipaddr% -t %kill-jar%

echo 开始启动新服务....
pause
cd /D E:\OpenSSH-Win64
ssh -p %ssh_port% -i %rsa_key_file% %ssh_user%@%ssh_ipaddr% -t %start-jar%